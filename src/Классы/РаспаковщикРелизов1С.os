// ----------------------------------------------------------
// This Source Code Form is subject to the terms of the
// Mozilla Public License, v.2.0. If a copy of the MPL
// was not distributed with this file, You can obtain one
// at http://mozilla.org/MPL/2.0/.
// ----------------------------------------------------------

Перем МенеджерОбработкиДанных;  // ВнешняяОбработкаОбъект - обработка-менеджер, вызвавшая данный обработчик
Перем Идентификатор;            // Строка                 - идентификатор обработчика, заданный обработкой-менеджером
Перем ПараметрыОбработки;       // Структура              - параметры обработки
Перем Лог;

Перем Приложение_Имя;           // Строка                 - имя приложения
Перем Приложение_Ид;            // Строка                 - идентификатор приложения
Перем Приложение_Версия;        // Строка                 - версия приложения

Перем ПутьКДистрибутиву;        // Строка                 - путь к дистрибутиву конфигурации 1С
Перем КаталогДляРаспаковкиEFD;  // Строка                 - каталог для распаковки шаблона конфигурации
Перем ФайлыДляРаспаковкиEFD;    // Массив(Строка)         - список файлов для распаковки из архива EFD дистрибутива
                                //                          конфигурации, если не указан, то распаковываются все файлы

Перем НакопленныеДанные;        // Массив(Структура)      - результаты обработки данных

#Область ПрограммныйИнтерфейс

// Функция - признак возможности обработки, принимать входящие данные
// 
// Возвращаемое значение:
//	Булево - Истина - обработка может принимать входящие данные для обработки;
//	         Ложь - обработка не принимает входящие данные;
//
Функция ПринимаетДанные() Экспорт
	
	Возврат Ложь;
	
КонецФункции // ПринимаетДанные()

// Функция - признак возможности обработки, возвращать обработанные данные
// 
// Возвращаемое значение:
//	Булево - Истина - обработка может возвращать обработанные данные;
//	         Ложь - обработка не возвращает данные;
//
Функция ВозвращаетДанные() Экспорт
	
	Возврат Истина;
	
КонецФункции // ВозвращаетДанные()

// Функция - возвращает список параметров обработки
// 
// Возвращаемое значение:
//	Структура                                - структура входящих параметров обработки
//      *Тип                    - Строка         - тип параметра
//      *Обязательный           - Булево         - Истина - параметр обязателен
//      *ЗначениеПоУмолчанию    - Произвольный   - значение параметра по умолчанию
//      *Описание               - Строка         - описание параметра
//
Функция ОписаниеПараметров() Экспорт
	
	Параметры = Новый Структура();
	
	ДобавитьОписаниеПараметра(Параметры,
	                          "Приложение_Имя",
	                          "Строка",
	                          Ложь,
	                          "",
	                          "имя конфигурации 1С");

	ДобавитьОписаниеПараметра(Параметры,
	                          "Приложение_Ид",
	                          "Строка",
	                          Ложь,
	                          "",
	                          "идентификатор конфигурации 1С");

	ДобавитьОписаниеПараметра(Параметры,
	                          "Приложение_Версия",
	                          "Строка",
	                          Ложь,
	                          "",
	                          "версия конфигурации 1С");

	ДобавитьОписаниеПараметра(Параметры,
	                          "ПутьКДистрибутиву",
	                          "Строка",
	                          Истина,
	                          "",
	                          "путь к дистрибутиву конфигурации 1С");

	ДобавитьОписаниеПараметра(Параметры,
	                          "КаталогДляРаспаковкиEFD",
	                          "Строка",
	                          Истина,
	                          "",
	                          "каталог для распаковки шаблона конфигурации");

	ДобавитьОписаниеПараметра(Параметры,
	                          "ФайлыДляРаспаковкиEFD",
	                          "Массив",
	                          Ложь,
	                          "",
	                          "список файлов для распаковки из архива EFD дистрибутива конфигурации,
	                          |если не указан, то распаковываются все файлы");

	Возврат Параметры;
	
КонецФункции // ОписаниеПараметров()

// Функция - Возвращает обработку - менеджер
// 
// Возвращаемое значение:
//	ВнешняяОбработкаОбъект - обработка-менеджер
//
Функция МенеджерОбработкиДанных() Экспорт
	
	Возврат МенеджерОбработкиДанных;
	
КонецФункции // МенеджерОбработкиДанных()

// Процедура - Устанавливает обработку - менеджер
//
// Параметры:
//	НовыйМенеджерОбработкиДанных      - ВнешняяОбработкаОбъект - обработка-менеджер
//
Процедура УстановитьМенеджерОбработкиДанных(Знач НовыйМенеджерОбработкиДанных) Экспорт
	
	МенеджерОбработкиДанных = НовыйМенеджерОбработкиДанных;
	
КонецПроцедуры // УстановитьМенеджерОбработкиДанных()

// Функция - Возвращает идентификатор обработчика
// 
// Возвращаемое значение:
//	Строка - идентификатор обработчика
//
Функция Идентификатор() Экспорт
	
	Возврат Идентификатор;
	
КонецФункции // Идентификатор()

// Процедура - Устанавливает идентификатор обработчика
//
// Параметры:
//	НовыйИдентификатор      - Строка - новый идентификатор обработчика
//
Процедура УстановитьИдентификатор(Знач НовыйИдентификатор) Экспорт
	
	Идентификатор = НовыйИдентификатор;
	
КонецПроцедуры // УстановитьИдентификатор()

// Функция - Возвращает значения параметров обработки
// 
// Возвращаемое значение:
//	Структура - параметры обработки
//
Функция ПараметрыОбработкиДанных() Экспорт
	
	Возврат ПараметрыОбработки;
	
КонецФункции // ПараметрыОбработкиДанных()

// Процедура - Устанавливает значения параметров обработки данных
//
// Параметры:
//	НовыеПараметры      - Структура     - значения параметров обработки
//
Процедура УстановитьПараметрыОбработкиДанных(Знач НовыеПараметры) Экспорт
	
	ПараметрыОбработки = НовыеПараметры;
	
	Приложение_Имя = "НеизвестнаяКонфигурация";
	Если ПараметрыОбработки.Свойство("Приложение_Имя") Тогда
		Приложение_Имя = ПараметрыОбработки.Приложение_Имя;
	КонецЕсли;

	Приложение_Ид = "unknown";
	Если ПараметрыОбработки.Свойство("Приложение_Ид") Тогда
		Приложение_Ид = ПараметрыОбработки.Приложение_Ид;
	КонецЕсли;

	Приложение_Версия = "0.0.0.0";
	Если ПараметрыОбработки.Свойство("Приложение_Версия") Тогда
		Приложение_Версия = ПараметрыОбработки.Приложение_Версия;
	КонецЕсли;

	ПутьКДистрибутиву = "";
	Если ПараметрыОбработки.Свойство("ПутьКДистрибутиву") Тогда
		ПутьКДистрибутиву = ПараметрыОбработки.ПутьКДистрибутиву;
	КонецЕсли;

	КаталогДляРаспаковкиEFD = "";
	Если ПараметрыОбработки.Свойство("КаталогДляРаспаковкиEFD") Тогда
		КаталогДляРаспаковкиEFD = ПараметрыОбработки.КаталогДляРаспаковкиEFD;
	КонецЕсли;

	ФайлыДляРаспаковкиEFD = "";
	Если ПараметрыОбработки.Свойство("ФайлыДляРаспаковкиEFD") Тогда
		ФайлыДляРаспаковкиEFD = ПараметрыОбработки.ФайлыДляРаспаковкиEFD;
	КонецЕсли;

КонецПроцедуры // УстановитьПараметрыОбработкиДанных()

// Функция - Возвращает значение параметра обработки данных
// 
// Параметры:
//	ИмяПараметра      - Строка           - имя получаемого параметра
//
// Возвращаемое значение:
//	Произвольный      - значение параметра
//
Функция ПараметрОбработкиДанных(Знач ИмяПараметра) Экспорт
	
	Если НЕ ТипЗнч(ПараметрыОбработки) = Тип("Структура") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если НЕ ПараметрыОбработки.Свойство(ИмяПараметра) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ПараметрыОбработки[ИмяПараметра];
	
КонецФункции // ПараметрОбработкиДанных()

// Процедура - Устанавливает значение параметра обработки
//
// Параметры:
//	ИмяПараметра      - Строка           - имя устанавливаемого параметра
//	Значение          - Произвольный     - новое значение параметра
//
Процедура УстановитьПараметрОбработкиДанных(Знач ИмяПараметра, Знач Значение) Экспорт
	
	Если НЕ ТипЗнч(ПараметрыОбработки) = Тип("Структура") Тогда
		ПараметрыОбработки = Новый Структура();
	КонецЕсли;
	
	ПараметрыОбработки.Вставить(ИмяПараметра, Значение);

	Если ВРег(ИмяПараметра) = ВРег("Приложение_Имя") Тогда
		Приложение_Имя = Значение;
	ИначеЕсли ВРег(ИмяПараметра) = ВРег("Приложение_Ид") Тогда
		Приложение_Ид = Значение;
	ИначеЕсли ВРег(ИмяПараметра) = ВРег("Приложение_Версия") Тогда
		Приложение_Версия = Значение;
	ИначеЕсли ВРег(ИмяПараметра) = ВРег("ПутьКДистрибутиву") Тогда
		ПутьКДистрибутиву = Значение;
	ИначеЕсли ВРег(ИмяПараметра) = ВРег("КаталогДляРаспаковкиEFD") Тогда
		КаталогДляРаспаковкиEFD = Значение;
	ИначеЕсли ВРег(ИмяПараметра) = ВРег("ФайлыДляРаспаковкиEFD") Тогда
		ФайлыДляРаспаковкиEFD = Значение;
	Иначе
		Возврат;
	КонецЕсли;
	
КонецПроцедуры // УстановитьПараметрОбработкиДанных()

// Процедура - устанавливает данные для обработки
//
// Параметры:
//	Данные      - Структура     - значения параметров обработки
//
Процедура УстановитьДанные(Знач ВходящиеДанные) Экспорт
	
КонецПроцедуры // УстановитьДанные()

// Процедура - выполняет обработку данных
//
Процедура ОбработатьДанные() Экспорт

	ФайлАрхиваEFD = Новый Файл(ОбъединитьПути(ПутьКДистрибутиву, "1cv8.efd"));
	
	Если НЕ ФайлАрхиваEFD.Существует() Тогда
		ВызватьИсключение СтрШаблон("Не найден файл дистрибутива конфигурации ""%1""", ФайлАрхиваEFD.ПолноеИмя);
	КонецЕсли;
	
	ПрочитатьОписаниеШаблонаИзМанифеста(ФайлАрхиваEFD);

	Лог.Информация("Начало распаковки архива EFD ""%1"" конфигурации ""%2"", версия ""%3"" в каталог ""%4"".",
	               ФайлАрхиваEFD.ПолноеИмя,
	               Приложение_Имя,
	               Приложение_Версия,
	               КаталогДляРаспаковкиEFD);
		
	КаталогДляРаспаковки = ОбъединитьПути(КаталогДляРаспаковкиEFD, Приложение_Ид, Приложение_Версия);

	Распаковщик.ОбеспечитьКаталог(КаталогДляРаспаковки);

	Распаковщик.РаспаковатьШаблонКонфигурации1С(ФайлАрхиваEFD.ПолноеИмя,
	                                            КаталогДляРаспаковки,
	                                            ,
	                                            Перечисления.ВариантыРаспаковкиКаталогов.БезОбщихКаталогов);

	ПутьКФайлуОписания = ОбъединитьПути(ПутьКДистрибутиву, "description.json");
	ФайлОписания = Новый Файл(ПутьКФайлуОписания);
	Если ФайлОписания.Существует() Тогда
		НовыйПутьКФайлуОписания = ОбъединитьПути(КаталогДляРаспаковки, "description.json");
		КопироватьФайл(ПутьКФайлуОписания, НовыйПутьКФайлуОписания);
	КонецЕсли;

	Лог.Информация("Распакован файл архива EFD ""%1""", ФайлАрхиваEFD.ПолноеИмя);

	ПродолжениеОбработкиДанныхВызовМенеджера(КаталогДляРаспаковки);

	ЗавершениеОбработкиДанныхВызовМенеджера();

КонецПроцедуры // ОбработатьДанные()

Процедура ПрочитатьОписаниеШаблонаИзМанифеста(ФайлАрхиваEFD)

	КаталогДляРаспаковки = ПолучитьИмяВременногоФайла();

	Распаковщик.РаспаковатьШаблонКонфигурации1С(ФайлАрхиваEFD.ПолноеИмя,
	                                            КаталогДляРаспаковки,
	                                            "1cv8.mft",
	                                            Перечисления.ВариантыРаспаковкиКаталогов.БезКаталогов);

	ПутьКМанифесту = ОбъединитьПути(КаталогДляРаспаковки, "1cv8.mft");

	ВремФайл = Новый Файл(ПутьКМанифесту);
	Если НЕ ВремФайл.Существует() Тогда
		Возврат;
	КонецЕсли;

	Чтение = Новый ЧтениеТекста(ПутьКМанифесту);

	ТекСтрока = Чтение.ПрочитатьСтроку();

	Пока ЗначениеЗаполнено(ТекСтрока) Цикл
		ДанныеСтроки = СтрРазделить(ТекСтрока, "=");
		Если ДанныеСтроки.Количество() < 1 Тогда
			Продолжить;
		КонецЕсли;

		Если ВРег(СокрЛП(ДанныеСтроки[0])) = ВРег("Name")
		   И (НЕ ЗначениеЗаполнено(Приложение_Имя)
		 ИЛИ Приложение_Имя = "НеизвестнаяКонфигурация") Тогда
			Приложение_Имя = СокрЛП(ДанныеСтроки[1]);
		КонецЕсли;

		Если ВРег(СокрЛП(ДанныеСтроки[0])) = ВРег("Destination")
		   И (НЕ ЗначениеЗаполнено(Приложение_Ид)
		 ИЛИ Приложение_Ид = "unknown") Тогда
			Приложение_Ид = СокрЛП(ДанныеСтроки[1]);
			Приложение_Ид = Сред(Приложение_Ид, СтрНайти(Приложение_Ид, "\") + 1);
		КонецЕсли;

		Если ВРег(СокрЛП(ДанныеСтроки[0])) = ВРег("Version")
		   И (НЕ ЗначениеЗаполнено(Приложение_Версия)
		 ИЛИ Приложение_Версия = "0.0.0.0") Тогда
			Приложение_Версия = СокрЛП(ДанныеСтроки[1]);
		КонецЕсли;
		
		ТекСтрока = Чтение.ПрочитатьСтроку();

	КонецЦикла;

	Чтение.Закрыть();

	УдалитьФайлы(КаталогДляРаспаковки);

КонецПроцедуры // ПрочитатьОписаниеШаблонаИзМанифеста()

Функция РезультатОбработки() Экспорт
	
	Возврат НакопленныеДанные;
	
КонецФункции // РезультатОбработки()

// Процедура - выполняет действия при окончании обработки данных
// и оповещает обработку-менеджер о завершении обработки данных
//
Процедура ЗавершениеОбработкиДанных() Экспорт
	
	Лог.Информация("[%1]: Завершение обработки данных.", ТипЗнч(ЭтотОбъект));

	ЗавершениеОбработкиДанныхВызовМенеджера();
	
КонецПроцедуры // ЗавершениеОбработкиДанных()

#КонецОбласти // ПрограммныйИнтерфейс

#Область СлужебныйПрограммныйИнтерфейс

// Функция - возвращает объект управления логированием
//
// Возвращаемое значение:
//  Объект      - объект управления логированием
//
Функция Лог() Экспорт
	
	Возврат Лог;

КонецФункции // Лог()

// Процедура - устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект описание команды
//
Процедура ОписаниеКоманды(Команда) Экспорт
	
	Команда.Опция("p path", "", "путь к дистрибутиву конфигурации 1С")
	       .ТСтрока()
	       .ВОкружении("YARD_RELEASES_DISTRIB_PATH");

	Команда.Опция("ep extract-path", "", "каталог для распаковки загруженного архива")
	       .ТСтрока()
	       .ВОкружении("YARD_RELEASES_EXTRACT_PATH");

	Команда.Опция("ef extract-files", "", "список файлов для распаковки из архива дистрибутива, разделенный ""|"",
	                                      |если не указан, то распаковываются все файлы")
	       .ТСтрока()
	       .ВОкружении("YARD_RELEASES_EXTRACT_FILES");

	Команда.Опция("n app-name", "", "имя конфигурации 1С")
	       .ТСтрока()
	       .ВОкружении("YARD_RELEASES_EXTRACT_FILES");

	Команда.Опция("i app-id", "", "идентификатор конфигурации 1С")
	       .ТСтрока()
	       .ВОкружении("YARD_RELEASES_EXTRACT_FILES");

	Команда.Опция("v app-version", "", "версия конфигурации 1С")
	       .ТСтрока()
	       .ВОкружении("YARD_RELEASES_EXTRACT_FILES");

КонецПроцедуры // ОписаниеКоманды()

// Процедура - запускает выполнение команды устанавливает описание команды
//
// Параметры:
//  Команда    - КомандаПриложения     - объект  описание команды
//
Процедура ВыполнитьКоманду(Знач Команда) Экспорт

	ВыводОтладочнойИнформации = Команда.ЗначениеОпции("verbose");

	ПараметрыПриложения.УстановитьРежимОтладки(ВыводОтладочнойИнформации);

	УстановитьПараметрОбработкиДанных("Приложение_Имя"           , Команда.ЗначениеОпции("app-name"));
	УстановитьПараметрОбработкиДанных("Приложение_Ид"            , Команда.ЗначениеОпции("app-id"));
	УстановитьПараметрОбработкиДанных("Приложение_Версия"        , Команда.ЗначениеОпции("app-version"));

	УстановитьПараметрОбработкиДанных("ПутьКДистрибутиву"        , Команда.ЗначениеОпции("path"));
	УстановитьПараметрОбработкиДанных("КаталогДляРаспаковкиEFD"  , Команда.ЗначениеОпции("extract-path"));

	ВремФайлы = СтрРазделить(Команда.ЗначениеОпции("extract-files"), "|", Ложь);
	УстановитьПараметрОбработкиДанных("ФайлыДляРаспаковкиEFD"    , ВремФайлы);

	ОбработатьДанные();

КонецПроцедуры // ВыполнитьКоманду()

#КонецОбласти // СлужебныйПрограммныйИнтерфейс

#Область СлужебныеПроцедурыВызоваМенеджераОбработкиДанных

// Процедура - выполняет действия обработки элемента данных
// и оповещает обработку-менеджер о продолжении обработки элемента
//
//	Параметры:
//		Элемент    - Произвольный     - Элемент данных для продолжения обработки
//
Процедура ПродолжениеОбработкиДанныхВызовМенеджера(Элемент)
	
	Если МенеджерОбработкиДанных = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерОбработкиДанных.ПродолжениеОбработкиДанных(Элемент, Идентификатор);
	
КонецПроцедуры // ПродолжениеОбработкиДанныхВызовМенеджера()

// Процедура - выполняет действия при окончании обработки данных
// и оповещает обработку-менеджер о завершении обработки данных
//
Процедура ЗавершениеОбработкиДанныхВызовМенеджера()
	
	Если МенеджерОбработкиДанных = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерОбработкиДанных.ЗавершениеОбработкиДанных(Идентификатор);
	
КонецПроцедуры // ЗавершениеОбработкиДанныхВызовМенеджера()

#КонецОбласти // СлужебныеПроцедурыВызоваМенеджераОбработкиДанных

#Область СлужебныеПроцедурыИФункции

// Процедура - добавляет описание параметра обработки
// 
// Параметры:
//     ОписаниеПараметров     - Структура      - структура описаний параметров
//     Параметр               - Строка         - имя параметра
//     Тип                    - Строка         - список возможных типов параметра
//     Обязательный           - Булево         - Истина - параметр обязателен
//     ЗначениеПоУмолчанию    - Произвольный   - значение параметра по умолчанию
//     Описание               - Строка         - описание параметра
//
Процедура ДобавитьОписаниеПараметра(ОписаниеПараметров
	                              , Параметр
	                              , Тип
	                              , Обязательный = Ложь
	                              , ЗначениеПоУмолчанию = Неопределено
	                              , Описание = "")
	
	Если НЕ ТипЗнч(ОписаниеПараметров) = Тип("Структура") Тогда
		ОписаниеПараметров = Новый Структура();
	КонецЕсли;
	
	ОписаниеПараметра = Новый Структура();
	ОписаниеПараметра.Вставить("Тип"                , Тип);
	ОписаниеПараметра.Вставить("Обязательный"       , Обязательный);
	ОписаниеПараметра.Вставить("ЗначениеПоУмолчанию", ЗначениеПоУмолчанию);
	ОписаниеПараметра.Вставить("Описание"           , Описание);
	
	ОписаниеПараметров.Вставить(Параметр, ОписаниеПараметра);
	
КонецПроцедуры // ДобавитьОписаниеПараметра()

#КонецОбласти // СлужебныеПроцедурыИФункции

#Область ОбработчикиСобытий

// Процедура - обработчик события "ПриСозданииОбъекта"
//
// Параметры:
//  Менеджер	 - МенеджерОбработкиДанных    - менеджер обработки данных - владелец
// 
// BSLLS:UnusedLocalMethod-off
Процедура ПриСозданииОбъекта(Менеджер = Неопределено)

	УстановитьМенеджерОбработкиДанных(Менеджер);

	Лог = ПараметрыПриложения.Лог();

	Приложение_Имя    = "НеизвестнаяКонфигурация";
	Приложение_Ид     = "unknown";
	Приложение_Версия = "0.0.0.0";

	Лог.Информация("[%1]: Инициализирован обработчик", ТипЗнч(ЭтотОбъект));

КонецПроцедуры // ПриСозданииОбъекта()
// BSLLS:UnusedLocalMethod-on

#КонецОбласти // ОбработчикиСобытий
