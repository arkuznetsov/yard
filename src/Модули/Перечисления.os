// ----------------------------------------------------------
// This Source Code Form is subject to the terms of the
// Mozilla Public License, v.2.0. If a copy of the MPL
// was not distributed with this file, You can obtain one
// at http://mozilla.org/MPL/2.0/.
// ----------------------------------------------------------
// Codebase: https://github.com/ArKuznetsov/yard/
// ----------------------------------------------------------

#Использовать "../Макеты"

Перем ВариантыРаспаковкиКаталогов Экспорт;

// Функция - возвращает значение перечисления по полному текстовому представлению
// вида: <имя перечисления>.<значение перечисления>
//   
Функция Значение(Знач ПутьКЗначению) Экспорт

	МассивПеречисления = СтрРазделить(ПутьКЗначению, ".");

	Попытка
		Возврат ЭтотОбъект[МассивПеречисления[0]][МассивПеречисления[1]];
	Исключение
		ВызватьИсключение СтрШаблон("Не найдено значение ""%1"" перечисления ""%2"":%3",
									МассивПеречисления[1],
									МассивПеречисления[0],
									ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;

КонецФункции // Значение()

// Процедура инициализирует значения перечислений
// из данных макета "Перечисления.json"
// 
Процедура Инициализация()

	ПутьКМакету = ПолучитьМакет("/Макеты/Перечисления");
	
	Чтение = Новый ЧтениеJSON();
	Чтение.ОткрытьФайл(ПутьКМакету, КодировкаТекста.UTF8);
	
	ДанныеМакета = ПрочитатьJSON(Чтение, Ложь);

	Для Каждого ТекПеречисление Из ДанныеМакета Цикл
		Попытка
			ЭтотОбъект[ТекПеречисление.Ключ] = Новый Структура();
		Исключение
			ВызватьИсключение СтрШаблон("Не найдено перечисление ""%1"":%2",
			                            ТекПеречисление.Ключ,
			                            ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
		Для Каждого ТекЗначение Из ТекПеречисление.Значение Цикл
			Попытка
				ЭтотОбъект[ТекПеречисление.Ключ].Вставить(ТекЗначение.Ключ, ТекЗначение.Значение);
			Исключение
				ВызватьИсключение СтрШаблон("Не найдено значение ""%1"" перечисления ""%2"":%3",
				                            ТекЗначение.Ключ,
				                            ТекПеречисление.Ключ,
				                            ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			КонецПопытки;
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры // Инициализация()

Инициализация();